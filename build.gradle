buildscript {
	ext {
		springBootVersion = '1.5.4.RELEASE'
        assertjVersion = '3.8.0'
        catchexceptionVersion = '1.4.4'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: "jacoco" 
apply plugin: 'org.springframework.boot'

jar {
	baseName = 'vida-sushi'
	version = '0.1.0'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	mavenLocal()
}


// Integration test
	
sourceSets {
    integrationTest {
        java.srcDir file('src/integrationtest/java')
        resources.srcDir file('src/integrationtest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
	description = 'Runs the integration tests.'
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
integrationTest.testLogging.showStandardStreams = true
	
// FIN Integration test

// Location sources

eclipse {
    classpath {
        file {
            whenMerged { 
                def lib = entries.find { it.path.contains 'elipcero-springdatarest-0.1.0.jar' }
                lib.javadocPath = fileReference(file('build/libs/SpringDataRest-javadoc.jar'))
                lib.sourcePath = fileReference(file('build/libs/SpringDataRest-sources.jar'))
            }
        }
    }
}

// FIN Location sources

dependencies {
	compileOnly('org.projectlombok:lombok')

    compile('org.passay:passay:1.3.0')

	compile('org.springframework.boot:spring-boot-starter-thymeleaf')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.security.oauth:spring-security-oauth2')
    compile('org.springframework.security:spring-security-jwt')
	compile('org.springframework.boot:spring-boot-starter-data-mongodb')
	compile('org.springframework.boot:spring-boot-starter-data-rest')
	
	compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
	
	compile('com.elipcero.springgeneral:elipcero-springgeneral:0.1.0')
	compile('com.elipcero.springsecurity:elipcero-springsecurity:0.1.0')
	compile('com.elipcero.springdata:elipcero-springdata:0.1.0')
	compile('com.elipcero.springdatarest:elipcero-springdatarest:0.1.0')
	
    testCompile("org.assertj:assertj-core:${assertjVersion}")
    testCompile("eu.codearte.catch-exception:catch-exception:${catchexceptionVersion}")	
	testCompile('com.elipcero.springtest:elipcero-springtest:0.1.0')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}
